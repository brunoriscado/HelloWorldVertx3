def generateSrcPath="src/main/java/generated-src"
def generatedSrcDir = file("src/main/java/generated-src")

sourceSets {
    main {
        java.srcDirs += generatedSrcDir
        output.dir(builtBy: 'generateServiceProxy', generateSrcPath)
    }
}

task generateServiceProxy(type: JavaCompile, description: 'Generates EBServiceProxies') {
    source = sourceSets.main.java
    classpath = configurations.compile
    destinationDir = generatedSrcDir
    options.compilerArgs = [

            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",

            "-AoutputDirectory=$generateSrcPath"
    ]
}

tasks.withType(JavaCompile){
    options.compilerArgs << "-parameters"
}

test {
    afterTest { desc, result ->
        def time = result.endTime - result.startTime
        println "Test [${desc.className}] ${desc.name}: ${result.resultType} in ${time}ms"
    }
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} -->  ${result.testCount} tests ( ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped ) "
            }
        }
    }
}

jar {
    from sourceSets.main.allSource
    baseName = this.properties['mavenArtifactId']
    group = this.properties['group']
    version = this.properties['version']
}

task fatJar(type: Jar) {
    manifest {
        attributes('Main-Class': 'io.vertx.core.Launcher',
                'Main-Verticle': 'com.tesco.disco.browse.Starter')
    }
    baseName = this.properties['mavenArtifactId']
    group = this.properties['group']
    version = this.properties['version']
    classifier = 'all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName = this.properties['mavenArtifactId']
    group = this.properties['group']
    version = this.properties['version']
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives(fatJar, sourcesJar)
}

task generatePom << {
    pom {
    }.writeTo("$buildDir/libs/" + this.properties['mavenArtifactId'] + "-$project.version" + ".pom")
}

compileJava() {
    dependsOn generatePom
}







// Release Plugin configuration ends

def configurePom(def pom) {
    pom.project {
        name rootProject.name
        description 'Description of your module here'
        inceptionYear '2013'
        packaging 'jar'

        url 'Your project url'

        developers {
            developer {
                id 'developer id (e.g. github username)'
                name 'developer name'
                email 'developer email'
            }
        }

        scm {
            url 'url to your repo - e.g. github repo url'
        }

        licenses {
            license {
                name 'The name of the license used, e.g.: The Apache Software License, Version 2.0'
                url 'URl to the license, e.g.: http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        properties {
            setProperty('project.build.sourceEncoding', 'UTF8')
        }
    }
}




if (!hasProperty('userName')) {
    ext.userName = ''
}
if (!hasProperty('password')) {
    ext.password = ''
}
if (!hasProperty('nexusReleaseRepo')) {
    ext.nexusReleaseRepo = "http://releases"
}

if (!hasProperty('nexusSnapshotsRepo')) {
    ext.nexusSnapshotsRepo = "http://snapshots"
}


// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// maven task configuration

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    group 'build'
    description = "Does a maven deploy of archives artifacts"

    repositories {
        mavenDeployer {
            // setUniqueVersion(false)

            configuration = configurations.archives

            repository(url: "$nexusReleaseRepo") {
                authentication(userName: userName, password: password)
            }

            snapshotRepository(url: "$nexusSnapshotsRepo") {
                authentication(userName: userName, password: password)
            }

            pom.artifactId = "$mavenArtifactId"

            if (isReleaseVersion) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            configurePom(pom)
        }
    }
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// configuration methods